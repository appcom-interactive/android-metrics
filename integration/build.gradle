// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext.kotlin_version = '1.3.61'
  repositories {
    mavenLocal()
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.3'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.2'
    classpath 'com.dicedmelon.gradle:jacoco-android:0.1.4'

    classpath 'com.github.ben-manes:gradle-versions-plugin:0.27.0'
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.1.1'

    classpath 'de.nanogiants.gradle:android-metrics:0.0.3-SNAPSHOT'
  }
}

plugins {
  id "io.gitlab.arturbosch.detekt" version "1.4.0"
}

apply plugin: 'de.nanogiants.gradle.android-metrics'

allprojects {
  apply plugin: 'com.github.ben-manes.versions'

  repositories {
    mavenLocal()
    google()
    jcenter()
  }

  dependencyUpdates {
    resolutionStrategy = {
      componentSelection { rules ->
        rules.all { ComponentSelection selection ->
          boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any {
            qualifier -> selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
          }
          if (rejected) {
            selection.reject('Release candidate')
          }
        }
      }
    }
    outputFormatter = "json"
  }
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"
  apply plugin: "io.gitlab.arturbosch.detekt"
}

metrics {
  ignoreModules = ['base']
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
